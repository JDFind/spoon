buildscript {
  ext.versions = [
      'minSdk': 14,
      'compileSdk': 27,
      'buildTools': '27.0.3',

      'supportLibrary': '27.0.2',
      'kotlin': '1.2.60',
      'errorProne': '2.3.1',
  ]

  ext.deps = [
      'gson': 'com.google.code.gson:gson:2.8.5',
      'argparser': 'com.xenomachina:kotlin-argparser:2.0.2',
      'commonsLang3': 'org.apache.commons:commons-lang3:3.7',
      'commonsIo': 'commons-io:commons-io:2.6',
      'ddmlib': 'com.android.tools.ddms:ddmlib:26.1.3',
      'animatedGifLib': 'com.madgag:animated-gif-lib:1.4',
      'lesscss': 'org.lesscss:lesscss:1.7.0.1.1',
      'mustache': 'com.github.spullara.mustache.java:compiler:0.9.5',
      'jacocoMavenPlugin': 'org.jacoco:jacoco-maven-plugin:0.7.8',
      'butterknife': [
          'runtime': 'com.jakewharton:butterknife:8.8.1',
          'compiler': 'com.jakewharton:butterknife-compiler:8.8.1'
      ],
      'kotlinStdLibJre8': "org.jetbrains.kotlin:kotlin-stdlib-jre8:${versions.kotlin}",

      'junit': 'junit:junit:4.12',
      'truth': 'com.google.truth:truth:0.42',

      'support': [
          'annotations': "com.android.support:support-annotations:${versions.supportLibrary}",
          'appCompat': "com.android.support:appcompat-v7:${versions.supportLibrary}",
          'design': "com.android.support:design:${versions.supportLibrary}",
          'constraintLayout': "com.android.support.constraint:constraint-layout:1.0.2",

          'test': [
              'espresso': 'com.android.support.test.espresso:espresso-core:3.0.1',
              'espressoContrib': 'com.android.support.test.espresso:espresso-contrib:3.0.1',
              'runner': 'com.android.support.test:runner:1.0.1',
              'rules': 'com.android.support.test:rules:1.0.1',
          ],
      ],
  ]

  dependencies {
    classpath 'com.android.tools.build:gradle:3.1.3'
    classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.4'
    classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.11'
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
  }

  repositories {
    google()
    jcenter()
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
}

subprojects { project ->
  group = GROUP
  version = VERSION_NAME

  repositories {
    google()
    jcenter()
  }

  apply plugin: 'checkstyle'
  project.checkstyle {
    toolVersion "7.6.1"
    configFile rootProject.file('checkstyle.xml')
  }

  if (!project.path.startsWith(':third-party')) {
    project.apply(plugin: 'net.ltgt.errorprone')
  }

  configurations.all {
    resolutionStrategy {
      eachDependency { details ->
        // Force all of the primary support libraries to use the same version.
        if (details.requested.group == 'com.android.support') {
          details.useVersion versions.supportLibrary
        }
        // Force all the error-prone dependencies to use the same version.
        if (details.requested.group == 'com.google.errorprone'
            && details.requested.name.startsWith('error_prone_')) {
          details.useVersion versions.errorProne
        }
      }
    }
  }
}
